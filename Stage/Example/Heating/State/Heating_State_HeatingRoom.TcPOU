<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Heating_State_HeatingRoom" Id="{195b0e92-992c-4b31-a0dd-1b5af906cbe2}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Heating_State_HeatingRoom EXTENDS Heating_State
VAR CONSTANT
	EXIT_STEP		: USINT := 7;
	READY_STEP		: USINT := 8;
END_VAR
VAR
	_nStep		: USINT;
	_tonTimeout	: TON;
	_sErrMsg	: Str;
	_iNextState : I_State;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="CanActivate" Id="{f4e469e0-3329-4636-829f-7a5f4c27939b}">
      <Declaration><![CDATA[METHOD CanActivate : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CanActivate :=  _ctx.state.iMgr.Current = _ctx.state.iEnable;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnEntry" Id="{dc76118f-322c-4257-9d92-65205e9e8301}">
      <Declaration><![CDATA[METHOD OnEntry
VAR
	strb			: Strb;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_ctx.hmi.sStatusText := strb.Append('Heating enabled: Pumping to room ')
							.Append(UDINT_TO_STRING(_ctx.priv.iActiveRoom.Id))
							.Append('...')
							.Read();
							
_ctx.hmi.nStatusColor := COL.BLACK.Raw;

_ctx.hmi.sButtonText := 'Press to DISABLE!';
_ctx.hmi.nButtonColor := COL.BLACK.Raw;

_ctx.io.out.bBurner := FALSE;
_ctx.io.out.bPump := FALSE;

_nStep := 0;
_tonTimeout(IN := FALSE);

_iNextState := _ctx.state.iEnable;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExecute" Id="{bbc510df-aef5-44dc-9d86-57811004103f}">
      <Declaration><![CDATA[METHOD OnExecute
VAR
	strb		: Strb;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _ctx.priv.iActiveRoom = 0 THEN
	_iNextState := _ctx.state.iFault;	

	_nStep := EXIT_STEP;
	_ctx.hmi.sErrorText := 'HeatingRoom - Incorrect iActiveRoom value!';
ELSIF _ctx.hmi.bButton THEN
	_ctx.hmi.bButton := FALSE;
	_iNextState := _ctx.state.iDisable;
	
	_nStep := 4;
ELSIF _tonTimeout.Q THEN
	_ctx.priv.iActiveRoom.GoFault(strb.Set('Timeout on: ').Append(_sErrMsg).Read());
	
	_nStep := EXIT_STEP;
ELSIF _nStep < 4 AND_THEN NOT _ctx.priv.iActiveRoom.IsActive THEN
	_iNextState := _ctx.state.iEnable;	

	_nStep := 4;
END_IF

IF _ctx.priv.iActiveRoom <> 0 THEN
	_tonTimeout(IN := TRUE, PT := _ctx.priv.iActiveRoom.Timeout);
END_IF

CASE _nStep OF
	0:
		_sErrMsg := 'Valve opening!';
		_ctx.priv.iActiveRoom.GoHeat();
		_nStep := 1;
		
	1:
		IF _ctx.priv.iActiveRoom.IsOpen THEN
			_nStep := 2;
		END_IF
	2:	
		
		_ctx.io.out.bPump := TRUE;
		_nStep := 3;
	3:
		_sErrMsg := 'Heating!';
		IF _ctx.priv.iActiveRoom.IsReady THEN
			_nStep := 4;
		END_IF
	4:	
		_ctx.io.out.bPump := FALSE;
		_nStep := 5;
	5:
		_sErrMsg := 'Valve closing!';
		_ctx.priv.iActiveRoom.GoIdle();
		_nStep := 6;
	6:
		IF _ctx.priv.iActiveRoom.IsClosed THEN
			_nStep := EXIT_STEP;
		END_IF
	7:
		IF _iNextState <> 0 THEN
			_ctx.state.iMgr.Request(_iNextState);
		ELSE
			_ctx.hmi.sErrorText := 'HeatingRoom - Incorrect _iNextState value!';
			_ctx.state.iMgr.Request(_ctx.state.iFault);
		END_IF
		
		_nStep := READY_STEP;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExit" Id="{d4d44493-2d9f-4f23-8a2c-9424c1976ea7}">
      <Declaration><![CDATA[(*
Executed when the state transitions to another state.  
Provides a hook for defining cleanup or transition-specific behavior.
*)
METHOD OnExit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _ctx.priv.iActiveRoom <> 0 THEN
	_ctx.priv.iActiveRoom.GoIdle();
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>