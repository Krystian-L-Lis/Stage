<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Heating_State_Safety" Id="{b3c770ff-5618-4956-8c69-b75ebbd54c49}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Heating_State_Safety EXTENDS Heating_State IMPLEMENTS I_Callable, I_Init
VAR
	_init					: Init(THIS^);
	_safetyRec				: Receiver(THIS^);
	_bSafetyActive			: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Call" Id="{ccf8c9ca-547d-4a52-a859-566cad5f3a9e}">
      <Declaration><![CDATA[METHOD Call
VAR_INPUT
	iArg		: Core.I_Arg;
END_VAR
VAR
	_iSafeModeOn: I_BoolArg;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF IsOk(ArgToIBoolArg(iArg, iVal => _iSafeModeOn)) THEN
	IF _iSafeModeOn.Val THEN
		_bSafetyActive := TRUE;
		_ctx.state.iMgr.Request(_ctx.state.iSafety);
	ELSE
		_bSafetyActive := FALSE;
		_ctx.state.iMgr.Request(_ctx.state.iDisable);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CanDeactivate" Id="{fbb0ddd0-58f9-41c4-8e52-433fd6600485}">
      <Declaration><![CDATA[METHOD CanDeactivate : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CanDeactivate := NOT _bSafetyActive;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{a22836c3-bdf1-4b4e-b4df-40ad1a1ec5d1}">
      <Declaration><![CDATA[METHOD Init
VAR
	i			: Iter(1);
	_r			: Result;
	
	_iEntity	: I_Entity;
	_iSignal	: I_Signal;
	
	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE IsOk(GetEntity(i.Next(), iEntity => _iEntity)) DO
	IF IsOk(GetSignal(_iEntity, '*.onSafetyChange', iSignal => _iSignal)) THEN
		IF IsErr(_iSignal.Pair(_safetyRec), nOut => _r) THEN
			DebugMsg(_iSignal, CONCAT('Could not be paired! Error code: ', TO_STRING(_r)));
		END_IF
		
		EXIT;
	END_IF
END_WHILE

IF NOT IsOk(_r) THEN
	DebugMsg(THIS^, CONCAT('_safetyRec not paired! Error code: ', TO_STRING(_r)));
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnInit" Id="{c5598497-5be7-45e4-ae85-b40aa632e5cb}">
      <Declaration><![CDATA[METHOD OnInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_ctx.hmi.sStatusText := 'Safety engaged! Check safety system...';
_ctx.hmi.nStatusColor := COL.RED.Raw;

_ctx.hmi.sButtonText := '';
_ctx.hmi.nButtonColor := COL.INVISIBLE.Raw;

_ctx.io.out.bBurner := FALSE;
_ctx.io.out.bPump := FALSE;

_ctx.priv.iOnDisable.Emit();

]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>