<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="TankCtrl_Filling" Id="{366c1b23-b8d1-4c51-b3a7-d6286d6366d9}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TankCtrl_Filling EXTENDS _TankCtrl_State
VAR
	_nState		: USINT;
	_tonTimeout	: TON;
	
	iFill			: I_Tag;
	iIsFillable		: I_Flag;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="CanActivate" Id="{e2f42c0c-ae7d-4d35-9629-03dfce08b7cd}">
      <Declaration><![CDATA[METHOD CanActivate : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CanActivate := _ctx.priv.smgr.Current = _ctx.state.iEnabled;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnExecute" Id="{81f9ac50-35ba-4fd8-b79d-ad69e031e303}">
      <Declaration><![CDATA[METHOD OnExecute
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _ctx.hmi.bMultiButton THEN
	_ctx.hmi.bMultiButton := FALSE;
	
	_nState := 44;
	RETURN;
END_IF

CASE _nState OF
	0:
		IF IsOk(FindState(_ctx.priv.iTank.Smgr, 'Fill', iTag => iFill)) THEN
			_ctx.priv.iTank.Smgr.Request(iFill);
			_nState := 1;
		ELSE
			_ctx.priv.nErrorCode := 1;
			_nState := 99;
		END_IF
	1:
		_tonTimeout(IN := TRUE, PT := T#2S);
		IF _ctx.priv.iTank.Smgr.Current = iFill THEN
			_tonTimeout(IN := FALSE);
			_nState := 2;
		ELSIF _tonTimeout.Q THEN
			_tonTimeout(IN := FALSE);
			_ctx.priv.nErrorCode := 2;
			_nState := 99;
		END_IF
	2:
		IF IsOk(FindFlag(_ctx.priv.iTank.Flags, 'IsFillable', iFlag => iIsFillable)) THEN
			_nState := 3;
		ELSE
			_ctx.priv.nErrorCode := 3;
			_nState := 99;
		END_IF		
	3:
		_ctx.io.out.bRunPump := TRUE;
		_tonTimeout(IN := TRUE, PT := UDINT_TO_TIME(_ctx.hmi.nFillingTimeout.val * 1000));
		IF iIsFillable.IsLowered() THEN
			_tonTimeout(IN := FALSE);
			_nState := 4;
		ELSIF _tonTimeout.Q THEN
			_tonTimeout(IN := FALSE);
			_ctx.priv.nErrorCode := 4;
			_nState := 99;			
		END_IF
	4:
		_ctx.priv.smgr.Request(_ctx.state.iEnabled);
		_nState := 5;
	44:
		_ctx.priv.smgr.Request(_ctx.state.iDisabled);
		_nState := 45;		
	99:
		_ctx.priv.smgr.Request(_ctx.state.iFault);
		_nState := 100;
	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnInit" Id="{7158dc6c-ac4f-450c-af66-b517991c2291}">
      <Declaration><![CDATA[METHOD OnInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_nState := 0;
_tonTimeout(IN := FALSE);
_ctx.priv.nErrorCode := 0;

_ctx.priv.IsSystemOn.Rise();

_ctx.io.out.bRunPump := FALSE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>