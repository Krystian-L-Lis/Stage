<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FindManyFlags" Id="{1c51f7ea-21b4-4b3a-8f5e-704026d3f344}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION FindManyFlags : Results.Result
VAR_INPUT
	iFlagList		: I_FlagList;
	nResultPou		: ANY;
END_VAR
VAR_IN_OUT
	sToFind		: ARRAY[*] OF Strings.Str;
END_VAR
VAR
	i			: Utils.GenIter(0);
	j			: UDINT;
	cmp			: Tags.TagCmp;
	_iFlag		: I_Flag;
	_nLow		: UDINT := DINT_TO_UDINT(LOWER_BOUND(sToFind, 1));
	_nUp		: UDINT := DINT_TO_UDINT(UPPER_BOUND(sToFind, 1));
	_nResSize	: UDINT := DINT_TO_UDINT(nResultPou.diSize);
	_nExpLen	: UDINT := _nUp - _nLow + 1;
	_nExpSize	: UDINT := _nExpLen * SIZEOF(__UXINT);
	
	_nFound		: UDINT := 0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF iFlagList = 0 THEN
	FindManyFlags := Results.Err.Itf0;
	RETURN;
END_IF

// Ensure lower bound starts at zero
IF _nLow <> 0 THEN
	FindManyFlags := Results.Err.Ovf;
	RETURN;
END_IF

// Ensure result size matches expected size
IF _nResSize <> _nExpSize THEN
	FindManyFlags := Results.Err.NoMatch;
	RETURN;
END_IF

// Check if result pointer is null
IF nResultPou.pValue = 0 THEN
	FindManyFlags := Results.Err.Ptr0;
	RETURN;
END_IF

// Iterate over the flag list
WHILE Results.IsOk(iFlagList.GetByPos(i.Next(), iFlag => _iFlag)) DO
	cmp.Set(_iFlag.Tag);
	FOR j := _nLow TO _nUp DO
		// Match the string and copy if successful
		IF cmp.MatchPrtId(sToFind[j]).IsOk() THEN
			Tc2_System.MEMCPY(ADR(nResultPou.pValue) + SIZEOF(__UXINT) * j, ADR(_iFlag), SIZEOF(__UXINT));
			_nFound := _nFound + 1;
			sToFind[j] := '';  // Mark as found
			EXIT;  // Break after first match (since there should be only one)
		END_IF
	END_FOR;
	
	// Early exit if all entries are found (this condition can be removed if only one flag is expected)
	IF _nFound = (_nUp - _nLow + 1) THEN
		FindManyFlags := Results.Ok;
		RETURN;
	END_IF 
END_WHILE

// Final check to see if all entries were processed
FOR j := _nLow TO _nUp DO
	IF sToFind[j] <> '' THEN  // Check for unfound entries
		FindManyFlags := Results.Err.Udf;  // Error if any remain unfound (handled at higher level)
		RETURN;
	END_IF
END_FOR

// Return success if everything is found
FindManyFlags := Results.Ok;]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>