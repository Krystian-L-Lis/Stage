<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Math" Id="{79849915-ccca-45cc-a51a-353bb7315daa}" SpecialFunc="None">
    <Declaration><![CDATA[(*Provides various mathematical functions*)
PROGRAM Math
VAR
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="ExptDecay" Id="{8d3aa407-7a4a-435a-88e6-4592a7a47b65}">
      <Declaration><![CDATA[(* 
    Time-compensated smoothing coefficient. 
    Alpha = 1 - (1 - B)^dt
*)
METHOD ExptDecay : LREAL
VAR_INPUT
    rBlendFactor : LREAL;   // Blending factor [0, 1]
    rDeltaTime   : LREAL := 0.01;   // Sample time [s]
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[rBlendFactor := LIMIT(0.0, rBlendFactor, 1.0);
rDeltaTime   := MAX(rDeltaTime, 0.0);
ExptDecay := LIMIT(0.0, 1.0 - EXPT(1.0 - rBlendFactor, rDeltaTime), 1.0);]]></ST>
      </Implementation>
    </Method>
    <Method Name="FreqCutoffCoef" Id="{bd72f08d-8d4f-43e5-b950-5b0461356dd2}">
      <Declaration><![CDATA[(* 
    Time-compensated low-pass coefficient from cutoff frequency. 
    Alpha = 1 - exp(-2π · Fc · dt)
*)
METHOD FreqCutoffCoef : LREAL
VAR_STAT CONSTANT
	rA: LREAL := 2.0 * 3.141592653589793;
END_VAR
VAR_INPUT
    rCutoffHz  : LREAL;   // Cutoff frequency [Hz]
    rDeltaTime : LREAL;   // Sample time [s]
END_VAR
VAR
	rOmega : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[rCutoffHz  := MAX(rCutoffHz, 0.0);
rDeltaTime := MAX(rDeltaTime, 0.0);
rOmega := rA * rCutoffHz;
FreqCutoffCoef := 1.0 - EXP(-rOmega * rDeltaTime);
FreqCutoffCoef := LIMIT(0.0, FreqCutoffCoef, 1.0);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LinearScale" Id="{57ad8d01-8b3d-4c66-a03e-ccd8286efe23}">
      <Declaration><![CDATA[(*
Allows for value scalling.
Y = MinY + ((X − MinX) / (MaxX − MinX)) * (MaxY − MinY)
Returns 0 when rMaxX - rMinX = 0.
*)
METHOD LinearScale : LREAL
VAR_INPUT
	rX				: LREAL;
	rMinX			: LREAL;
	rMaxX			: LREAL;
	rMinY			: LREAL := 0;
	rMaxY			: LREAL := 1;
END_VAR
VAR
	_rDnmtr			: LREAL := rMaxX - rMinX;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _rDnmtr <> 0 THEN
	LinearScale := rMinY + ((rX - rMinX)/_rDnmtr) * (rMaxY - rMinY);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="LowPassFilter" Id="{8bf1a163-e19a-406a-9e5d-a23ea6bfa2ad}">
      <Declaration><![CDATA[(*
First-order low pass filter.
Yt = Xt * Coeff + Y(t-1) * (1- Coeff)
*)
METHOD LowPassFilter : LREAL
VAR_INPUT
	rX				: LREAL;
	rPrvY			: LREAL;
	
	rCoefficient	: LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[LowPassFilter := rX * rCoefficient + rPrvY * (1 - rCoefficient);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Random" Id="{446e7349-8d3a-44ca-b827-be8536ff03cb}">
      <Declaration><![CDATA[(*
Generates random number every time is called.
*)
METHOD Random : LREAL
VAR_STAT
	_nSeed		: INT;
END_VAR
VAR
	_drand		: Tc2_System.DRAND;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_drand(Seed := _nSeed, Num => Random);
_nSeed := _nSeed + 1;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>