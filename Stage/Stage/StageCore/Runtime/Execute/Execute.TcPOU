<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Execute" Id="{fd30bbbf-c4e7-4255-aff2-259a2cee51d5}" SpecialFunc="None">
    <Declaration><![CDATA[(*
Allows registration of FB implementing `I_Execute`. 
*)
FUNCTION_BLOCK FINAL Execute IMPLEMENTS I_ExecuteNode, I_Debug
VAR
	_iExecute		: I_Execute;
	_iNext			: I_ExecuteNode;
	_nState			: Result;
	_nPhase			: INT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Attach" Id="{aef47a4c-4a3d-4182-85d5-570cee5368fe}">
      <Declaration><![CDATA[METHOD INTERNAL Attach : Result
VAR_INPUT
	iExecute		: I_ExecuteNode;
END_VAR
VAR
	_iThis			: I_ExecuteNode := THIS^;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iExecute = 0 THEN
	NewDebug(THIS^, Param.INIT_INTERNAL_DEBUG_MODE, Tc2_Standard.CONCAT(__POUNAME(), ': Itf = 0!'));
	RETURN;
END_IF

IF _iThis <> _Runtime.iLastCyclic THEN
	NewDebug(THIS^, Param.INIT_INTERNAL_DEBUG_MODE, Tc2_Standard.CONCAT(__POUNAME(), ': Is not the last node!'));
	RETURN;
END_IF

IF IsSome(iExecute.state) THEN
	NewDebug(iExecute, Param.INIT_INTERNAL_DEBUG_MODE, Tc2_Standard.CONCAT(__POUNAME(), ': Has already been added!'));
	RETURN;
END_IF

_Runtime.iLastCyclic := iExecute;
_iNext := iExecute;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Exe" Id="{c801d5e7-2cd0-4874-a9b5-b0187da63b5c}">
      <Declaration><![CDATA[METHOD INTERNAL Exe
VAR_OUTPUT
	iNext: I_ExecuteNode;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_iExecute.Execute(_nPhase);
iNext := _iNext;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{0c4d921f-e0eb-4cc3-851c-9a1e40abf9a1}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
{attribute 'hide'}
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
	
	iExecute			: I_Execute;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF IsSome(_nState) THEN
	RETURN;
END_IF

IF (_iExecute := iExecute) = 0 THEN
	_nState := Err.Itf0;
	NewDebug(THIS^, Param.INIT_INTERNAL_DEBUG_MODE, Tc2_Standard.CONCAT(__POUNAME(), ': Itf = 0!'));
	RETURN;
END_IF



IF _Runtime.iLastCyclic <> 0 THEN
	IF IsErr(_Runtime.iLastCyclic.Attach(THIS^), nOut => _nState) THEN
		NewDebug(THIS^, Param.INIT_INTERNAL_DEBUG_MODE, Tc2_Standard.CONCAT(__POUNAME(), ': Error Inside!'));
	END_IF
ELSE
	_Runtime.iFirstCyclic := THIS^;
	_Runtime.iLastCyclic := THIS^;
	_nState := Ok;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="Phase" Id="{3dbfac59-95aa-4942-90ae-6bed7167a965}">
      <Declaration><![CDATA[PROPERTY Phase : INT]]></Declaration>
      <Get Name="Get" Id="{ee046bff-0af1-478c-bfb8-ca19ceb398e9}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Phase := _nPhase;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{893ba47b-0fa2-41e5-bb90-b0e5e1297555}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_nPhase := Phase;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="State" Id="{24ee4fd7-b9b8-4900-937c-af456b6522b2}">
      <Declaration><![CDATA[PROPERTY INTERNAL State : Result]]></Declaration>
      <Get Name="Get" Id="{c6d174a2-5343-443e-acc0-fa6de12655a3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _nState;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{cdaef5bc-3e80-45aa-8a2d-58cc8bd11cfe}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF IsNone(_nState) THEN
	_nState := State;
END_IF]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>