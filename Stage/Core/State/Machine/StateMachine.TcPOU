<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="StateMachine" Id="{570a94dc-a903-48df-83c5-e0703868b568}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FINAL StateMachine IMPLEMENTS I_StateMachine, I_Execute
VAR
	_exe					: Execute(THIS^);
	_iCurrentState			: I_StateInternal;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Change" Id="{c50228a6-90d0-4008-bdcd-b5914bd96cd5}">
      <Declaration><![CDATA[METHOD Change : Result
VAR_INPUT
	iNewState	: I_StateInternal;
END_VAR
VAR_OUTPUT
	iOldState	: I_StateInternal;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iNewState = 0 THEN
	Change := Err.Itf0;
	RETURN;
END_IF

IF iNewState = _iCurrentState THEN
	Change := None;
	RETURN;
END_IF

IF NOT iNewState.CanActivate() THEN
	Change := None;
	RETURN;
END_IF

IF _iCurrentState <> 0 AND_THEN NOT _iCurrentState.CanDeactivate() THEN
	Change := None;
	RETURN;
END_IF

IF _iCurrentState <> 0 THEN
	_iCurrentState.OnExit();
END_IF

iNewState.OnInit();
iOldState := _iCurrentState;
_iCurrentState := iNewState;
Change := Ok;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{b879bcd6-341c-4f09-8d48-6726ba706920}">
      <Declaration><![CDATA[METHOD Execute
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _iCurrentState <> 0 THEN
	_iCurrentState.OnExecute();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{3735c750-a73c-45d9-a9dd-2c943f6399af}">
      <Declaration><![CDATA[PROPERTY State : I_State]]></Declaration>
      <Get Name="Get" Id="{c5922222-7a05-464c-8714-df81e9760121}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _iCurrentState.AsIState;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>