<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="MatchPath" Id="{d88b0b61-1f65-4468-bff7-7ead93691118}" SpecialFunc="None">
    <Declaration><![CDATA[(*
Returns whether the symbol's path matches the provided pattern.
Allows wildcards in form of "*" that can cover multiple segments.
Example pattern: *.functionBlock.*.bBool
*)
FUNCTION MatchPath : Result
VAR_INPUT
    sAddress : Str;  /// The symbol's path to evaluate
    sPattern : Str;  /// The pattern to match against
END_VAR
VAR
	pttPath				: TagPath;
	pttSeg				: Str;
	pttPeek				: Str;
	addPath				: TagPath;
	addSeg				: Str;
	bMatch				: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Init
IF IsErr(addPath.Set(sAddress)) THEN
	MatchPath := Err.NoMatch;
	RETURN;
END_IF
IF IsErr(pttPath.Set(sPattern)) THEN
	MatchPath := Err.NoMatch;
	RETURN;
END_IF

// Advance to the next segment
WHILE IsOk(pttPath.Next(sSeg => pttSeg)) DO	
	bMatch := FALSE;
	// If the current segment is a wildcard, then peek to the next segment
	IF pttSeg = '*' THEN
		IF IsOk(pttPath.Peek(sSeg => pttPeek)) THEN
			IF pttPeek = '*' THEN
				CONTINUE; // Skip redundant wildcard if *.*.
			END_IF
		ELSE
			MatchPath := Ok;
			RETURN; // No more segments in the pattern, so it's a match as everything so far matched(.*)
		END_IF

	// If current segment is not a wildcard reset the peek.
	ELSIF pttSeg <> '*' THEN
		pttPeek := '';
	END_IF
	
	// Get the next segment of the address path
	WHILE IsOk(addPath.Next(sSeg => addSeg)) DO
		IF pttPeek <> '' THEN
			IF pttPeek = addSeg THEN
				bMatch := TRUE;
				pttPath.Next(); // Consume get next pattern segment
				EXIT;
			ELSE
				CONTINUE; // Get next address
			END_IF
		ELSIF pttSeg = addSeg THEN
			bMatch := TRUE;
			EXIT; // Consume get next pattern segment
		ELSE
			MatchPath := Err.NoMatch;
			RETURN; // No match - return
		END_IF
	END_WHILE

	IF NOT bMatch THEN
		MatchPath := Err.NoMatch;
		RETURN;
	END_IF

END_WHILE

IF IsOk(addPath.Peek()) THEN
	MatchPath := Err.NoMatch;
	RETURN;
END_IF

MatchPath := Ok;


]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>